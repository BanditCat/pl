////////////////////////////////////////////////////////////////////////////////
//     Copyright (c) Jon DuBois 2022. This file is part of pseudoluminal.     //
//                                                                            //
// This program is free software: you can redistribute it and/or modify       //
// it under the terms of the GNU Affero General Public License as published   //
// by the Free Software Foundation, either version 3 of the License, or       //
// (at your option) any later version.                                        //
//                                                                            //
// This program is distributed in the hope that it will be useful,            //
// but WITHOUT ANY WARRANTY; without even the implied warranty of             //
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the              //
// GNU Affero General Public License for more details.                        //
//                                                                            //
// You should have received a copy of the GNU Affero General Public License   //
// along with this program.  If not, see <https://www.gnu.org/licenses/>.     //
//                                                                            //
////////////////////////////////////////////////////////////////////////////////
// Compute shader.                                                            //
////////////////////////////////////////////////////////////////////////////////

#version 460

#extension GL_EXT_control_flow_attributes : enable
#include "interface.glib"


layout(constant_id = 1) const float epsilon = 0;
layout(constant_id = 2) const float maxTime = 0;


struct particle{
  vec4 pos;
  vec4 vel;
};

layout(constant_id = 3) const uint ocdim = 0;
layout(constant_id = 4) const uint ocdepth = 0;
layout(constant_id = 5) const uint ocnodeCount = 1;
layout(constant_id = 6) const float ocscale = 0.0;

const uint ocdatasize = ( ocdim * ocdim * ocdim ) / 4;
struct oc{
  // pos.w is particle count
  vec4 pos;
  vec4 mass;
  vec4 data[ ocdatasize ];
};






layout( local_size_x_id = 0 ) in;


layout( binding = 1 ) writeonly restrict buffer oparticleBuffer{
  particle ops[];
};
layout( binding = 2 ) readonly restrict buffer iparticleBuffer{
  particle ips[];
};
layout( binding = 3 ) coherent restrict buffer octree{
  uint next;
  oc tree[ ocnodeCount ];
};

void addParticle( particle p ){
  float scale = ocscale;
  uint index = 0;
  [[unroll]]for( uint i = 0; i < ocdepth; ++i ){
    vec3 mpos = ( p.pos.xyz - tree[ index ].pos.xyz ) / scale;
    float mdim = ( ocdim - 1 ) / 2.0;
    mpos = floor( mpos * vec3( mdim ) + vec3( mdim ) );
    vec3 ncenter = ( mpos - vec3( mdim ) ) / vec3( mdim );
    ncenter = ncenter * scale + tree[ index ].pos.xyz;
    uint nindex = uint(dot( mpos, vec3( 1, ocdim, ocdim * ocdim ) ));
    uint ncomp = nindex % 4;
    nindex /= 4;
      
    float tmass = tree[ index ].mass.w + p.pos.w;
    tree[ index ].mass = vec4( ( tree[ index ].mass.xyz * tree[ index ].mass.w +
				 p.pos.xyz * p.pos.w ) / tmass, tmass );
    
    if( tree[ index ].data[ nindex ][ ncomp ] == 0 ){
      next += 1;
      tree[ index ].data[ nindex ][ ncomp ] = next;
      tree[ next ].pos = vec4( ncenter, 0 );
      tree[ next ].mass = vec4( 0 );
      for( uint j = 0; j < ocdatasize; ++j )
	tree[ next ].data[ j ] = vec4( 0 );
      index = next;
    } else
      index = uint( tree[ index ].data[ nindex ][ ncomp ] );
    scale /= ocdim;
  }
  
}


void main(){
  float td = deltaTime * 14;
  if( td > maxTime )
    td = maxTime;

  // octree
  if( gl_GlobalInvocationID.x == 0 ){
    tree[ 0 ].pos = vec4( 0 );
    tree[ 0 ].mass = vec4( 0 );
    for( uint j = 0; j < ocdatasize; ++j )
      tree[ 0 ].data[ j ] = vec4( 0 );

    addParticle( ips[ 0 ] );
  }

  
  const uint size = gl_NumWorkGroups.x * gl_WorkGroupSize.x;
  vec3 force = vec3( 0 );
  uint j = gl_GlobalInvocationID.x;
  // [[unroll]]for( uint i = 0; i <; ++i )
  //   shr[ i ] = ips[ i ];
  // barrier();
  float jmass = ips[ j ].pos.w;
  float jrad = pow( jmass, 0.3333333333333 );
  float transfer = 0;
  [[unroll]]for( uint i = 0; i < size; ++i ){
    if( j != i ){
      float imass = ips[ i ].pos.w;
      float irad = pow( imass, 0.3333333333333 );
      vec3 dif = ips[ i ].pos.xyz - ips[ j ].pos.xyz;
      float dist = pow( dot( dif, dif ) + epsilon, 1.5 );
      if( dist - ( irad + jrad ) * 1 < 0.005 ){
	transfer += ( jmass - imass ) / 4000000;
      }
      vec3 fvec = ( dif * imass ) / dist;
      force += fvec;
    }
  }
  force *= 0.00000009;
  vec3 mforce = force * td * td;
  vec3 mvel = ips[ j ].vel.xyz;
  vec3 npos = ips[ j ].pos.xyz + mvel + mforce;
  npos = mod( npos + 50, 100 ) - 50; 
  ops[ j ].pos = vec4( npos, clamp( jmass + transfer, 0, 1000000000 ) );
  ops[ j ].vel.xyz = clamp( mvel + td * force, -0.1, 0.1 );
  // [[unroll]]for( uint i = 0; i <  gl_WorkGroupSize.; ++i )
  //   ops[ i ] = shr[ i ];
}
 
